import org.jetbrains.kotlin.gradle.plugin.mpp.NativeOutputKind

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.0-dev-1"
    }
}

apply plugin: 'kotlin-multiplatform'

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation 'com.example:sample-lib:1.0'
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }
        allJvm {
            dependsOn commonMain
            dependencies {
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.23.4'
            }
        }
        jvm6Main.dependsOn allJvm
        jvm8Main.dependsOn allJvm
    }

    targets {
        fromPreset(presets.jvm, 'jvm6') {
            compilations.main.source(sourceSets.allJvm)
        }
        fromPreset(presets.jvm, 'jvm8') {
            compilations.main.source(sourceSets.allJvm)
        }
        fromPreset(presets.js, 'nodeJs')

        fromPreset(presets.linuxX64, 'linux')
        fromPreset(presets.mingwX64, 'windows')
        fromPreset(presets.macosX64, 'mac')

        configure([linux, windows, mac]) {
            compilations.main.outputKinds += NativeOutputKind.EXECUTABLE
        }
    }
}
